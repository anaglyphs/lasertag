// https://github.com/oculus-samples/Unity-DepthAPI/issues/16
#include "DepthKit.hlsl"

Texture2DArray<float> inputDepthTex;
RWTexture2DArray<float> agDepthTexRW;
RWTexture2DArray<float4> agDepthNormalTexRW;
RWTexture2D<half> agDepthReadback;

int eye = 0;

#pragma kernel DepthCopy

[numthreads(8, 8, 1)]
void DepthCopy(uint3 id : SV_DispatchThreadID)
{
	agDepthTexRW[id] = inputDepthTex[id];
}

#pragma kernel DepthNorm

[numthreads(8, 8, 1)]
void DepthNorm(uint3 id : SV_DispatchThreadID)
{
	float2 texSizeF = agDepthTexSize;
	
	uint2 ind = id.xy;
	float2 uv = id.xy / texSizeF;
	uint eye = id.z;
	
	float3 depthWorld = agDepthNDCtoWorld(float3(uv, agDepthTex[uint3(ind, eye)]), eye);
	
	ind = id.xy + uint2(1, 0);
	uv = ind / texSizeF;
	float3 depthWorldH = agDepthNDCtoWorld(float3(uv, agDepthTex[uint3(ind, eye)]), eye);

	ind = id.xy + uint2(0, 1);
	uv = ind / texSizeF;
	float3 depthWorldV = agDepthNDCtoWorld(float3(uv, agDepthTex[uint3(ind, eye)]), eye);
	
	const float3 hDeriv = depthWorldH - depthWorld;
	const float3 vDeriv = depthWorldV - depthWorld;
	
	float3 worldNorm = -normalize(cross(hDeriv, vDeriv));

    agDepthNormalTexRW[id] = float4(worldNorm, 1);
}

#pragma kernel ConvertDepth

[numthreads(8, 8, 1)]
void ConvertDepth(uint2 id : SV_DispatchThreadID)
{
	agDepthReadback[id] = agDepthTex[uint3(id, 0)];
}

//#pragma kernel ConvertRGBA32toR8

//Texture2D<float4> rgbaTex;
//RWTexture2D<float> rTex;

//[numthreads(8, 8, 1)]
//void ConvertRGBA32toR8(uint2 id : SV_DispatchThreadID)
//{
//	agDepthTex[uint3(id, 0)] = rgbaTex[id].r;
//}